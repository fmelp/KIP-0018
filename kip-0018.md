---
KIP: 0018
Title: Smart Wallet Standard
Author: Francesco Melpignano @fmelp, Randy Daal @randynamic
Status: Draft
Type: Standard
Category: Interface
Created: 2023-02-27
---

# Abstract

We propose a `smart wallet` interface with the goal of providing developers on the Kadena ecosystem with a design for a smart contract wallet that provides more features than a standard single or multi-sig EOA (externally owned account).


# Motivation

EOA wallets are limited in their functionality and are a hurdle to mainstream adoption of blockchain technology. The goal of this KIP is to propose a blockchain wallet design that leverages Pact smart contracts and delivers the necessary UX and DX needed to onboard mainstream web users.


## Use-cases

- Increase account recovery options with concept of a master key
    1. use a traditional seed phrase
    2. rely on a centralized and regulatated custodian -- such as a bank or government entity
    3. use social recovery -- a multi-sig keyset that could contain keys from friends and family or unused devices
    4. multi-party computation [MPC](https://www.fordefi.com/)
    5. tiered approach to recovery depending on key security
        - traditional methods for less secure keys (email / authenticator / sms / oauth)
- Create familiar experiences, such as the banking app concept of a savings vs checking account
    1. less secure (webauthN) key can only identify account, but not spend tokens (all non-financial usecases)
    2. less secure (webauthN) key can only spend x tokens / day
    3. spending more than x tokens per day would need signing from multiple devices (phone, watch, laptop)
    4. spending all your balance would need a signature from the master key
- KYC platform
    1. Make it easier to identify KYC'd accounts for protocols that require it
- No-blockhain expereince
    1. use gas-stations funded at wallet creation to abstract away concept of gas for end users
    2. have users use a blockchain without knowing and without understanding any underlying tech concepts

## Competitive Landscape
- Ethereum community has engaging conversations around smart wallets, but has yet to see implementations for the following reasons:
    1. Gas fee issues -> far more expensive than using EOA
    2. Limitations of web3.js library with `connect wallet` paradigm
    3. Does not bring that many extra features over EOA
- Others:
    1. Industry leaders seem to want to make their stack compatible with popular wallets, such as metamask, but not fix the root of the wallet problem: bad UX and DX


# Specification

## Acccount Usage
1. **Account Usage**
    1. Authentification → Mainstream UX
        1. No more seed phrase
        2. 2FA (sms/other)
        3. Password with email
        4. OAuth providers ([torus](https://github.com/kadena-community/torus-wallet))
        5. WebauthN ([repo](https://github.com/kadena-io/webauthn-parsers/tree/feature/example))
        6. Hardware keys (on user device and yubikey)
    2. Features → More than any wallet on market
        1. No gas for end user
            1. Pact gas-station built-in
        2. Multi-sig transactions
            1. owned or external accounts
        3. Predefined transaction limits (checking vs savings accounts)
            1. static ([const limit code](https://github.com/kadena-io/pact-examples/blob/smart-wallet/smart-wallets/my-smart-wallet/my-smart-wallet.pact))
            2. dynamic ([n-key method code](https://github.com/kadena-io/pact-examples/blob/smart-wallet/smart-wallets/my-smart-wallet/n-key-wallet.pact))
        4. Whitelist / blacklist
            1. send only to known addresses
            2. auto-deny unwanted addresses
        5. Trusted Guardians
            1. master-key concept
            2. bypass account limits
            3. seedless account recovery
        6. Bundled transactions
            1. Sign once for many tx’s
        7. Safe transfer
            1. Ensure only intended receiver gets funds
    3. Account Recovery → beyond seed phrases
        1. Device freezing and rotation
            1. think netflix multiple device approval
        2. Social Recovery
            1. include non-cryptographic recovery
        3. Centralized recovery (trusted institution)
            1. use trusted guardian / master-key concept
        4. Emergency Account management
            1. special features if devices lost / stolen / compromised
2. **On-Chain features**
    1. Automated DeFi
        1. One-stop shop to manage on-chain portfolio
        2. Can automate trading and position management
    2. NFT
        1. View all NFT status
        2. Buy / sell NFTs
3. **Off-chain features**
    1. Advanced Alerts
        1. Give users granular info about wallet activity
        2. Set up custom alerts / notifications
    2. KYC management
        1. toggle between KYC’d and anon accounts
    3. Tax management
    4. Finish cross-chain transactions
    5. Chain Abstraction
    6. WalletConnect v2
    7. Detailed transaction history

# Examples

### Smart Contract Wallet 

```
(define-keyset 'master-keyset (read-keyset 'master-keyset))
;(namespace 'user)
(module my-smart-wallet GOVERNANCE
  @doc"'smart-wallet' provides safe transactions of Kadena coins. \
  \The accounts are guarded by the module guard, there fore GOVERNANCE \
  \of the contract will be the master key of all accounts with complete control.\
  \Accounts will each have guards that can make transfers under certain rules. \
  \When using the master key, none of these restrictions apply. "

  (defcap GOVERNANCE()
    (enforce-keyset (keyset-ref-guard 'master-keyset)))

  ; --------------------------------------------------------------------------
  ; Schemas and Tables

  (defschema wallet
    @doc "schema of accounts and customized guards"
    guard:guard
    max-withdrawal:decimal
    min-balance:decimal
    time-between:decimal
    last-withdrawal-time:time
    )

  (deftable wallet-table:{wallet})

  ; --------------------------------------------------------------------------
  ; Constants

  (defconst DEFAULT_MAX_WITHDRAWAL:decimal 20.0)
  (defconst DEFAULT_MIN_BALANCE:decimal 20.0)
  (defconst DEFAULT_TIME_BETWEEN 1800.0)
  (defconst EPOCH (time "1970-01-01T00:00:00Z"))

  ; --------------------------------------------------------------------------
  ; Module Guards

  (defun wallet-module-guard:guard()
    @doc "module guard of the wallet."

    (create-module-guard 'wallet-guard))

  ;;Control with master key

  (defun rotate-guard:string (account:string new-guard:guard)
    @doc "rotates the account guard"

    (enforce-guard new-guard)
    (with-capability (GOVERNANCE)
      (update wallet-table account {
        "guard": new-guard
        })
    )
  )


  (defun update-min-balance:string (account:string amount:decimal)
    @doc "updates the minimum balance an account needs to hold"
    (with-capability (GOVERNANCE)
      (update wallet-table account {
        "min-balance": amount
      })
    )
  )

  (defun update-max-withdrawal:string (account:string amount:decimal)
    @doc "updates the maximum balance an account can transfer"
    (with-capability (GOVERNANCE)
      (update wallet-table account {
        "max-withdrawal": amount
      })
    )
  )


  (defun update-time-between:string (account:string time-between:time)
    @doc "updates the minimum time an account has to wait between two transfers."
    (with-capability (GOVERNANCE)
      (update wallet-table account {
        "time-between": time-between
      })
    )
  )

  ; --------------------------------------------------------------------------
  ; Capabilities

  (defcap TRANSFER:bool (account:string amount:decimal)
    @doc "enforces the customized rules of the account"
    (with-read wallet-table account {
        "guard":= guard,
        "max-withdrawal":= max-amount,
        "min-balance":= min-bal,
        "time-between":= time-between,
        "last-withdrawal-time":= last-withdrawal-time
      }
      (enforce ( >= max-amount amount) "Transfer amount is too big")
      (enforce ( >= (diff-time (curr-time) last-withdrawal-time) time-between)
        (format "You're transferring too many times {} {}" [time-between (diff-time (curr-time) last-withdrawal-time)]))
      (let (( coin-balance:decimal (coin.get-balance account) ))
        (enforce ( <= min-bal (- coin-balance amount)) (format "min bal {} and this left {}" [min-bal (- coin-balance amount)]))
      )
      (enforce-guard guard)
      (update wallet-table account {
        "last-withdrawal-time": (curr-time)
        })
    ))

  (defun transfer (account receiver amount)
    @doc "Runs coin.transfer with customized rules."
    (with-capability (TRANSFER account amount)
      (coin.transfer account receiver amount)
    )
  )

  (defun transfer-create (account receiver receiver-guard amount)
    @doc "Runs coin.transfer-create with customized rules."
    (with-capability (TRANSFER account amount)
      (coin.transfer-create account receiver receiver-guard amount)
    )
  )

  (defun safe-transfer (sender:string receiver:string receiver-guard:guard amount:decimal extra:decimal)
    @doc "Transfer between two known accounts. The sender has to be the account from the wallet-table."
    (with-capability (TRANSFER sender amount)
      (coin.transfer-create sender receiver receiver-guard (+ amount extra))
      (coin.transfer receiver sender extra))
  )

  ; --------------------------------------------------------------------------
  ; Smart Wallet Functions

  (defun create-wallet-account:string (
      account:string
      guard:guard
    )
    @doc "Inserts an account into the wallet table safely. The account will have default customization"

    (enforce-guard guard)
    (insert wallet-table account {
      "guard": guard,
      "max-withdrawal": DEFAULT_MAX_WITHDRAWAL,
      "min-balance": DEFAULT_MIN_BALANCE,
      "time-between": DEFAULT_TIME_BETWEEN,
      "last-withdrawal-time": EPOCH
    })
    (coin.create-account account (wallet-module-guard))
    )

  (defun curr-time ()
    (at 'block-time (chain-data))
  )
)


(create-table wallet-table)
```

### Max Withdrwal Limit 

```
(module max-limit-smart-wallet GOVERNANCE
  @doc"'smart-wallet' provides safe transactions of Kadena coins. \
  \Users will have low-security and high-security transactions with this wallet with two keysets. \
  \Low-security keyset will be encrypted and saved by the software, while high-security keysets  \
  \are entirely managed by the user. For convenient usage, users can set maximum amount of    \
  \ low-security transactions and allow small transactions with low-seucrity keys. For bigger       \
  \transactions, users are required to provide high-security keys"

  (use coin)

  (defcap GOVERNANCE()
    (enforce false "Enforce non-upgradeability except in the case of a hard fork"))

  ; --------------------------------------------------------------------------
  ; Schemas and Tables

  (defschema wallet
    low-security-guard:guard ;keyset managed by software
    high-security-guard:guard ;;keyset managed by the user
    max-low-security-amount:decimal;Maximum amount of transfer allowed with low-security keyset
    low-security-withdrawal-time-limit:decimal
    last-low-security-withdrawal-time:time
    )

  (deftable wallet-table:{wallet})

  ; --------------------------------------------------------------------------
  ; Module Guards

  (defun wallet-module-guard:guard()
    (create-module-guard 'wallet-guard))

  ; --------------------------------------------------------------------------
  ; Capabilities

  (defcap LOW_SECURITY:bool (account)
    @doc "Lookup and enforce low-security guards associated with an account"
    (with-read wallet-table account {
        "low-security-guard":= guard
      }
      (enforce-guard guard)
      ))

  (defcap HIGH_SECURITY:bool (account)
    @doc "Lookup and enforce high-security guards associated with an account"
    (with-read wallet-table account {
        "high-security-guard":= guard
      }
      (enforce-guard guard)))

  (defcap AMOUNT_CHECK:bool (account:string amount:decimal)
    @doc "Lookup and enforce that the tx amount is in the range of allowed low-security tx amount"

    (with-read wallet-table account {
      "max-low-security-amount":= max
      }
      (enforce (> max amount) "Amount is bigger than maximum")))

  (defcap TIME_CHECK:bool (account:string)
    @doc "Lookup and enforce low-security guards associated with an account"
    (with-read wallet-table account {
        "low-security-withdrawal-time-limit":= time-limit,
        "last-low-security-withdrawal-time":= last-withdrawal-time}
    (enforce (>=
      (diff-time (curr-time) last-withdrawal-time)
      time-limit)
      "Withdrawal time limit has not met" )))

  ; --------------------------------------------------------------------------
  ; Constants

  (defconst EPOCH (time "1970-01-01T00:00:00Z"))

  ; --------------------------------------------------------------------------
  ; Smart Wallet Functions

  (defun create-wallet-account:string (
    account:string
    low-security-guard:guard
    high-security-guard:guard
    max-low-security-amount:decimal
    low-security-withdrawal-time-limit:decimal)
    @doc "Create a coin account with ACCOUNT and WALLET_MODULE_GUARD in coin-table,     \
    \and create a wallet account with ACCOUNT, LOW_SECURITY_GUARD, and HIGH_SECURITY_GUARD,\
    \ and MAX_LOW_SECURITY_AMOUNT "

    (enforce-guard low-security-guard)
    (enforce-guard high-security-guard)

    ;;Create Account in Kadena Coin Table with Wallet Module Guard.
    ;;This Prevents other contracts from tx functions of accounts created by the wallet.
    (create-account account (wallet-module-guard))
    (insert wallet-table account {
        "low-security-guard": low-security-guard,
        "high-security-guard": high-security-guard,
        "max-low-security-amount": max-low-security-amount,
        "low-security-withdrawal-time-limit": low-security-withdrawal-time-limit,
        "last-low-security-withdrawal-time": EPOCH
       }))

  (defun low-security-transfer:string (sender:string receiver:string amount:decimal)
    @doc "This function enforces that tx amount is smaller than SENDER's    \
    \MAX_LOW_SECURITY_AMOUNT, and then enforces the SENDER's HIGH_SECURITY_GUARD. \
    \ If the conditions qualify, transfers AMOUNT of Kadena Coin from SENDER \
    \account to RECEIVER account."

    (with-capability (LOW_SECURITY sender)
      (with-capability (TIME_CHECK sender)
        (with-capability (AMOUNT_CHECK sender amount)
          (transfer sender receiver amount)
          (update wallet-table sender {
            "last-low-security-withdrawal-time":(curr-time)
            })))))

  (defun high-security-transfer:string (sender:string receiver:string amount:decimal)
    @doc "This function enforces SENDER's HIGH_SECURITY_GUARD and transfers \
    \ AMOUNT of Kadena coins from SENDER account to RECEIVER account without \
    \limit."

    (with-capability (HIGH_SECURITY sender)
      (transfer sender receiver amount)))

  (defun update-max-low-security-amount:string (account:string amount:decimal)
    @doc "This function enforces ACCOUNT's high-security keyset and updates \
    \ACCOUNT's max-low-security-amount to AMOUNT"

    (with-capability (HIGH_SECURITY account)
      (update wallet-table account {
        "max-low-security-amount": amount
        })))

  (defun update-low-security-withdrawal-time-limit:string (account:string withdrawal-time-limit:decimal)
    @doc "This function enforces ACCOUNT's high-security keyset and updates \
    \ACCOUNT's low-security-withdrawal-time-limit to WITHDRAWAL_TIME_LIMIT"

    (with-capability (HIGH_SECURITY account)
      (update wallet-table account {
        "low-security-withdrawal-time-limit": withdrawal-time-limit
        })))

  (defun rotate-low-security-guard:string (account:string guard:guard)
    @doc "This function enforces ACCOUNT's low-security-guard and presented GUARD\
    \ and replaces ACCOUNT's low-security-guard with GUARD"

    (enforce-guard guard)
    (with-capability (LOW_SECURITY account)
      (update wallet-table account {
        "low-security-guard": guard
        })))

  (defun rotate-high-security-guard:string (account:string guard:guard)
    @doc "This function enforces ACCOUNT's high-security-guard and presented GUARD \
    \ and replaces ACCOUNT's high-security-guard with GUARD"

    (enforce-guard guard)
    (with-capability (HIGH_SECURITY account)
      (update wallet-table account {
        "high-security-guard": guard
        })))

  (defun curr-time ()
    (at 'block-time (chain-data))
  )
)

(create-table wallet-table)
```

### Dynamic Withdrawl Limit
```
(define-keyset 'admin-keyset (read-keyset 'admin-keyset))

(module n-key-wallet 'admin-keyset
  @doc"'n-key-wallet' provides safe transactions of Kadena coins.                \
  \ The accounts in the wallet are guarded by a list of keys and a guardian key. \
  \ Each transfer will require n number of keys to be signed depending on the    \
  \ ratio of (amount to transfer) : (total balance). For example, if an account  \
  \ has a balance of 10.0 coins and guard list with 2 keys, then the transfer    \
  \ of 0-5 coins will require 1 key to be signed, and the transfer of 5-10 coins \
  \ will require 2 keys to be signed. Guardian key will allow the rotation of    \
  \ guard list in case some keys are lost."

  (defschema user
    user:string
    guards:list
    guardian:guard)

  (deftable user-table:{user})

  (defun smart-guard:bool ()
    @doc "Module guard"
    (create-module-guard 'wallet-guard)
    )

  (defun create-smart-user (user:string guards:[guard] guardian:guard)
    @doc "Create a coin account managed by customized smart wallet"
    (enforce-guard guardian)
    (insert user-table user {
      "user": user,
      "guards": guards,
      "guardian": guardian
      })
    (coin.create-account user (smart-guard))
  )

  (defun smart-transfer (account receiver amount)
    @doc "Runs coin.transfer with customized rules."
    (enforce-signed account amount)
    (coin.transfer account receiver amount))

  (defun rotate-guards (user:string guards:[guard])
    @doc "Rotate guards of the user"
    (enforce-guardian user)
    (update user-table user {
      "guards": guards
      }))

  (defun rotate-guardian-guard (user:string guardian:guard)
    @doc "Rotate guards of the user"
    (enforce-guardian user)
    (update user-table user {
      "guardian": guardian
      }))

 ;;Utility functions
 (defun enforce-guardian:bool (user:string)
  @doc "Enforced the guardian guard"
   (enforce-guard (guardian user))
 )

 (defun enforce-signed:bool (user amount)
   @doc "Enforced customized rule to find what guards need to be signed."
   (let* (
     (needed (numKeysNeeded user amount))
     (signed (numKeysSigned user)))
     (enforce (<= needed signed)
       (format "{} more keys need to be signed"
       [(- needed signed)]))))

  (defun numKeysNeeded:integer (user:string amount:decimal)
    @doc "Number of keys required to transfer amount"
    (let ((
      balance (total-balance user)
      ))
    (enforce (<= amount balance) "Insufficient balance")
    (logic (numKeys user) amount balance)))

  (defun numKeysSigned:integer (user:string)
    @doc "Number of keys signed"
    (length
      (filter
        (= true)
          (map (try-enforce-guard) (guards user)))))

  (defun try-enforce-guard:bool (guard:guard)
    @doc "Try enforcing a guard for transfer and returns result"
    (try false (enforce-guard guard)))

  (defun logic:integer (numKeys:integer amount:decimal total:decimal)
    @doc "Default logic to calculate number of keys needed to transfer amount"
    (ceiling (* numKeys (/ amount total))))

  (defun numKeys (user:string)
    (length (guards user)))

  (defun guards (user:string)
    (at 'guards (read user-table user)))

  (defun guardian (user:string)
    (at 'guardian (read user-table user)))

  (defun total-balance (user:string)
    (at 'balance (coin.details user)))
)

(create-table user-table)
```

# **Backwards Compatibility**

- Need to figure out best practices to add tokens to a module-guard that is not just native KDA
- How to integrate into systems that expect users to have a convenctional `k:` account

# **References**

- Previous smart wallet [repo](https://github.com/kadena-io/pact-examples/tree/smart-wallet/smart-wallets) (thanks Hee Kyun!)
- Ethereum smart wallet explanations [one](https://decommas.io/blog/smart-contract-wallets-explained), [two](https://cryptotesters.com/blog/what-are-smart-contract-wallets), [three](https://blog.makerdao.com/what-are-smart-contract-wallets-and-how-can-they-benefit-defi-users/), [four](https://www.argent.xyz/learn/what-is-a-smart-contract-wallet/)

# **Potential next steps**

- Define features of smart wallet
- Define functions to implement
- Decide on interface or other method for standard
- Decide on a single smart contract or one-contract-per-wallet paradigm
